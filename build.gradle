buildscript {
    ext.ios_framework_name = 'StatusCodes'
}
plugins {
    id 'kotlin-multiplatform' version '1.3.21'
}
repositories {
    mavenCentral()
}
group 'com.example'
version '0.0.1'

apply plugin: 'maven-publish'


kotlin {
    jvm()

    targets {
        fromPreset(presets.iosArm64, 'ios') {
            binaries {
                framework("$ios_framework_name") {
//                    embedBitcode('disable')
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        iosMain {
        }
        iosTest {
        }
    }
}

configurations {
    compileClasspath
}

task packForXCode {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String buildType = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    def keyFrameworkPrefix = "$ios_framework_name${buildType.toLowerCase().capitalize()}"
    dependsOn "link${keyFrameworkPrefix}FrameworkIos"

    doLast {
        def srcFile = kotlin.targets.ios.binaries.getFramework("$ios_framework_name", buildType).outputFile

        copy {
            from srcFile.parent
            into frameworkDir
        }

        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
        
    }
}

task refreshArchive(type: Zip) {
    doFirst {
        delete fileTree(rootProject.rootDir) {
            include 'StatusCodes.zip'

        }
    }
    
    from "${rootProject.rootDir}/build/xcode-frameworks/"
    include '*/**' //to include contents of a folder present inside Reports directory
    archiveName 'StatusCodes.zip'
    destinationDir file(rootProject.rootDir)
}


tasks.build.dependsOn packForXCode
